10 'KOMPLEKSILUKUJEN LASKIN
20 '(c) Tommi Rautava
30 '941012-941013 / 950719 v3A
80 MODE4:ZS=0:FIIS=0:SYOTE$=""
90 PRINT"Kompleksilukujen laskin";CHR$(13);
100 INPUT"Anna luku >",SYOTE$
110 GOSUB 500
115 IF SYOTE$="" THEN GOSUB300:END
120 ZS=Z:FIIS=FII
130 INPUT"Anna luku >",SYOTE$
140 GOSUB 500
150 IF SYOTE$="" THEN GOSUB300:END
160 GOSUB700:GOSUB300
190 GOTO 130
300 Z=ZS:FII=FIIS:GOSUB650:GOSUB600:ZS=Z:FIIS=FII:SET F3
310 RE=REC(ZS,FIIS):IM=Y
320 PRINT RE;"+";IM;"j";CHR$(13);
330 PRINT ZS;"/";FIIS;CHR$(223)
340 SET N:RETURN
490 END
500 RE=VAL(SYOTE$):L=LEN(STR$(RE))
510 IF RE<0 THEN L=L+1
520 MRK=ASC(MID$(SYOTE$,L,1))
530 IF MRK=43 OR MRK=45 OR MRK=47 THEN 550
540 Z=RE:FII=0:RETURN
550 IF MRK<>47 THEN L=L-1
555 IM=VAL(MID$(SYOTE$,L+1,LEN(SYOTE$)-L))
560 IF MRK=47 THEN Z=RE:FII=IM:GOTO 570
565 Z=POL(RE,IM):FII=Y
570 GOSUB650:GOSUB600
590 RETURN
600 IF FII>360 THEN FII=FII-360:GOTO 600
610 IF FII<-360 THEN FII=FII+360:GOTO 610
620 RETURN
650 IF Z<0 THEN Z=-Z:FII=FII+180
660 RETURN
700 INPUT"Laskutoimitus >",MRK$
705 M=ASC(MID$(MRK$,1,1))
710 IF M=42 OR M=43 OR M=45 OR M=47 OR M=49 GOTO 720:ELSE GOTO 700
720 IF M=42 THEN ZS=ZS*Z:FIIS=FIIS+FII
730 IF M=47 THEN ZS=ZS/Z:FIIS=FIIS-FII
740 IF M=43 THEN GOSUB 800:GOSUB850
750 IF M=45 THEN GOSUB800:RE2=-RE2:IM2=-IM2:GOSUB850
760 IF M=49 THEN Z=1/Z:FII=-FII:GOSUB650:GOSUB600:PRINT"1/Z OK":GOTO700
790 RETURN
800 RE1=REC(ZS,FIIS):IM1=Y
805 GOSUB650:GOSUB600
810 RE2=REC(Z,FII):IM2=Y
820 RETURN
850 RE1=RE1+RE2:IM1=IM1+IM2
855 IF RE1=0 AND IM1=0 THEN ZS=0:FIIS=0:RETURN
860 ZS=POL(RE1,IM1):FIIS=Y
870 RETURN
