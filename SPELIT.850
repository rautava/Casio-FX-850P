1 'SPEDEN SPELIT -TARKASTIN
2 '(c) Tommi Rautava
3 '950706 / 950824 v4A
5 CLEAR
6 SPELI$="SPELI"
7 NIMI$="-- Speden spelit - tarkastin --"
10 GOSUB600:GOSUB 2000
15 GOSUB 3000:PRINT"Odota hetki ";
30 DIM A(SARAKKEITA,10):DIM B(SARAKKEITA)
40 GOSUB 700
90 GOSUB 3000
100 FOR R=1 TO 10
110 PRINT "Anna numero";R;
120 INPUT ">",N
130 FOR S=1 TO SARAKKEITA
140 IF A(S,R)=N THEN B(S)=B(S)+1
145 PRINT B(S);
150 NEXT S
160 PRINT
170 NEXT R
205 FOR S=1 TO SARAKKEITA
210 IF B(S)>=6 THEN PRINT "Sarakkeessa";S;"-";B(S);"oikein!":OK=OK+1
220 NEXT S
230 IF OK=0 THEN PRINT"Ei yhtaan voittoa, valitan.";:PRINT
299 END
600 'LASKE SPELI-SARAKKEET
605 SARAKKEITA=0:RESTORE#
610 RESTORE# SPELI$,0,800
620 READ# TEMP$,N
630 SARAKKEITA=SARAKKEITA+1
640 GOTO 610
700 'HAE SPELI-RIVIT TAULUKKOON
701 RESTORE#
705 FOR S=1 TO SARAKKEITA
710 RESTORE# SPELI$,0
720 READ# TEMP$,N
730 FOR R=10 TO 1 STEP -1
740 A(S,R)=N MOD 10
750 N=N/10
760 NEXT R
765 PRINT".";
770 NEXT S
775 PRINT
780 RETURN
800 'TARK. ETTA SARAKKEITA ON
805 IF SARAKKEITA>0 THEN RETURN
810 GOSUB 3000:PRINT"Ei yhtaan Spelia?! - EXE"
820 GOTO10
850 'KYSY SARAKE
860 GOSUB 3000:PRINT"Sarakkeen";SARAKKEITA;"nrot >";:INPUT"",N
870 RETURN
900 'LUE JA KIRJ. SARAKE MEMOON
905 SARAKKEITA=SARAKKEITA+1:GOSUB 850
910 RESTORE#:M=0
915 RESTORE# "",0,950
920 READ# TEMP$:M=M+1
930 GOTO 915
950 N$=MID$(STR$(N),2,10)
960 TEMP$=SPELI$+","+N$
970 WRITE# TEMP$
980 RETURN
1000 'POISTA SPELIT MEMOSTA
1005 RESTORE#
1010 RESTORE# SPELIT$,0,1090
1020 WRITE# ""
1030 GOTO 1010
1090 GOSUB600
1095 GOSUB 3000:PRINT"Poisto valmis - paina EXE"
1099 RETURN
2000 GOSUB 3000:PRINT SARAKKEITA;"(N)ollaa (L)isays (V)almis";
2010 C$=INKEY$
2020 IF C$="N" OR C$="n" THEN GOSUB 1000:GOTO 2000
2030 IF C$="L" OR C$="l" THEN GOSUB 900:GOTO 2000
2040 IF C$="V" OR C$="v" THEN RETURN
2050 GOTO 2010
3000 CLS:PRINT NIMI$;:PRINT:RETURN
